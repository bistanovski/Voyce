/*!
    \page model-components.html tutorial
    \title Model Components
    \startpage How to create mobile application for Podcast streaming using Felgo SDK

    \previouspage Main Components
    \nextpage ListView and Delegate Components

    The model components of the application, and their implementation is presented
    in this page.

    \section1 DataModelConnections component

    DataModelConnections is simple processing-of-signals component, in which
    signal handlers (slots) are implemented. These slots further take actions
    on global data models, settings and downloaders.

    \b{DataModelConnections.qml:}

    \qml
        import QtQuick 2.0
        import Felgo 3.0

        Connections {
            id: rootConnections

            onClearTags: {
                _dataHolder.tags = []
            }

            onClearPodcasts: {
                _dataHolder.podcasts = []
            }

            onFetchPodcastTags: {
                function onSuccess(data) {
                    _dataHolder.tags = data;
                }
                function onError(error) {
                    console.log('Fetching PodcastTags failed:', error)
                }

                api.getPodcastTags(numOfTags, onSuccess, onError);
            }

            onFetchPodcastsForTag: {
                function onSuccess(data) {
                    _dataHolder.podcasts = data;
                }
                function onError(error) {
                    console.log('Fetching Podcasts failed:', error);
                }

                api.getPodcastsForTag(tag, numOfPodcasts, onSuccess, onError);
            }

            onAddToPlaylist: {
                _dataHolder.playlistUpdated = true;
                _dataHolder.playlistModelData.insert(0, episodeData);
            }

            onRemoveFromPlaylist: {
                _dataHolder.playlistUpdated = true;
                _dataHolder.playlistModelData.remove(episodeIndex, 1);
            }

            onClearAndSaveSettings: {
                var writeGranted = true;
                if(Theme.isAndroid) {
                    writeGranted = VoyceFileUtils.requestAndroidPermissions();
                    if(!writeGranted) {
                        nativeUtils.displayMessageBox(qsTr("Permissions not granted!"));
                    }
                }

                if(writeGranted && _dataHolder.playlistUpdated) {
                    voyceSettings.clearPreviousSavedPlaylist();
                    voyceSettings.savePlaylist();
                }

                if(writeGranted && _dataHolder.downloadsUpdated) {
                    voyceSettings.clearPreviousSavedDownloads();
                    voyceSettings.saveDownloads();
                }
            }

            onDownloadEpisode: {
                var writeGranted = true;

                if(Theme.isAndroid) {
                    writeGranted = VoyceFileUtils.requestAndroidPermissions();
                    if(!writeGranted) {
                        nativeUtils.displayMessageBox(qsTr("Permissions not granted!"));
                    }
                }

                if(writeGranted) {
                    episodeDownloader.downloadEpisode(episodeData);
                }
            }

            onDownloadFinished: {
                _dataHolder.downloadsUpdated = true;
                _dataHolder.downloadsModelData.insert(0, episodeData);
            }

            onRemoveFromDownloads: {
                var deleteGranted = true;
                if(Theme.isAndroid) {
                    deleteGranted = VoyceFileUtils.requestAndroidPermissions();
                    if(!deleteGranted) {
                        nativeUtils.displayMessageBox(qsTr("Permissions not granted!"));
                    }
                }

                if(deleteGranted && VoyceFileUtils.deleteFile(localUrl)) {
                    console.log('Episode Deleted!')
                    _dataHolder.downloadsUpdated = true;
                    _dataHolder.downloadsModelData.remove(episodeIndex, 1);
                }
            }
        }
    \endqml


    \section1 DataModel component
    \l{DataModel} - Documented properties
    \b{DataModel.qml:}

    \qml
        import QtQuick 2.0
        import Felgo 3.0
        import "../settings"

        Item {
            id: root

            property alias dispatcher: dataConnection.target
            readonly property bool isBusy: api.busy

            readonly property alias tags: _dataHolder.tags
            readonly property alias podcasts: _dataHolder.podcasts
            readonly property alias playlistModel: _dataHolder.playlistModelData
            readonly property alias downloadsModel: _dataHolder.downloadsModelData

            property alias mainSettings: voyceSettings

            VoyceSettings {
                id: voyceSettings
            }

            RestAPI {
                id: api
            }

            Item {
                id: _dataHolder
                property var tags: []
                property var podcasts: []

                property alias playlistModelData: playlistModel
                property bool playlistUpdated: false

                property alias downloadsModelData: downloadsModel
                property bool downloadsUpdated: false

                readonly property string keyFieldValue: "url"
                readonly property var fieldValues: ["url", "title", "pubDate", "length"]

                JsonListModel {
                    id: playlistModel
                    keyField: _dataHolder.keyFieldValue
                    fields: _dataHolder.fieldValues
                }

                JsonListModel {
                    id: downloadsModel
                    keyField: _dataHolder.keyFieldValue
                    fields: _dataHolder.fieldValues
                }
            }

            EpisodeDownloader {
                id: episodeDownloader
            }

            DataModelConnections {
                id: dataConnection
            }
        }
    \endqml


    \section1 FilterProxyModel component

    FilterProxyModel provides reusable SortFilterProxyModel instance, with exported
    properties for source model, search pattern and target role.


    \l{FilterProxyModel} - Documented properties
    \b{FilterProxyModel.qml:}

    \qml
        import Felgo 3.0

        SortFilterProxyModel {
            id: filterProxyModel

            property var targetModel
            property string searchValue
            property string targetRoleName

            sourceModel: targetModel
            filters: [
                AnyOf {
                    enabled: searchValue !== ''
                    RegExpFilter {
                        roleName: targetRoleName
                        pattern: searchValue
                        caseSensitivity: Qt.CaseInsensitive
                    }
                }
            ]
        }
    \endqml
*/
